#import inner;

BinJSONBool {
    Bool:val
}

BinJSONDouble {
    Q:val
}

BinJSONInt {
    Z:val
}

BinJSONList {
    [BinJSON]:items
}

BinJSONNull {

}

BinJSONObj {
    [Str]:keys,
    [BinJSON]:values
}

BinJSONStr {
    Str:val
}

BinJSON {
    BinJSONBool?:bool,
    BinJSONDouble?:double,
    BinJSONInt?:int,
    BinJSONList?:list,
    BinJSONNull?:null_,
    BinJSONObj?:obj,
    BinJSONStr?:str
}

BinJSON#parse_BinJSON[Str:data] {
    if ([data].len == 0 || data[0] != 'B') {
        abort "Invalid BinJSON start";
    };

    [Str]:strs = [Str] [];
    ParsingState:state = ParsingState [1, strs];
    W:string_count = take_VW_uint[data][state];
    for (W:i to string_count) {
        W:string_len = take_VW_uint[data][state];
        [strs].append[take_bytes[data][state][string_len]];
    };

    Byte:initial_id = take_byte[data][state];
    BinJSON:result = parse_BinJSON_val[initial_id][data][state];

    if ([data].len != state.i) {
        abort "Invalid BinJSON: parsing finished; more data avaliable";
    };

    return result;
}

Bool#[BinJSON:binjson].get_bool {
    return [binjson.bool].unwrap.val;
}

Q#[BinJSON:binjson].get_double {
    return [binjson.double].unwrap.val;
}

Z#[BinJSON:binjson].get_int {
    return [binjson.int].unwrap.val;
}

[BinJSON]#[BinJSON:binjson].get_list {
    return [binjson.list].unwrap.items;
}

Bool#[BinJSON:binjson].is_null {
    return ![binjson.null_].is_null;
}

BinJSONObj#[BinJSON:binjson].get_obj {
    return [binjson.obj].unwrap;
}

Str#[BinJSON:binjson].get_str {
    return [binjson.str].unwrap.val;
}

BinJSON#[BinJSONObj:obj].get[Str:key] {
    if ([obj.keys].len != [obj.values].len) {
        abort "BinJSONObj number of keys and values must match";
    };
    for (W:i enumerating obj.keys) {
        if ([obj.keys[i]] equals [key]) {
            return obj.values[i];
        };
    };
    abort "Requested key '{}' not found" % key;
}

Bool#[BinJSONObj:obj].contains_key[Str:target_key] {
    for (Str:key in obj.keys) {
        if ([key] equals [target_key]) {
            return true;
        };
    };
    return false;
}

BinJSON#[BinJSON:obj].get[Str:key] {
    return [[obj].get_obj].get[key];
}

Bool#[BinJSON:obj].contains_key[Str:target_key] {
    return [[obj].get_obj].contains_key[target_key];
}
